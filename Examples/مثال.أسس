اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/تـطبيق"؛
اشمل "مـتم/نـم"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/ProgArg"، "بـروجارج.أسس")؛
مـحا.اشمل_ملف("Alusus/I18n"، "تـرجمة.أسس")؛

استخدم مـتم؛

دالة اطبع_معطيات(المطعيات: مـصفوفة[نـص]) {
    عرف ع: صـحيح؛
    لكل ع = 0, ع < المطعيات.هات_الطول(), ++ع {
        طـرفية.اطبع("المطعيات[%d] = %s\n", ع, المطعيات(ع).صوان)؛
    }
}

دالة اطبع_خيارات(الخيارات: تـطبيق[نـص, نـص]) {
    عرف ع: صـحيح؛
    لكل ع = 0, ع < الخيارات.هات_الطول(), ++ع {
        طـرفية.اطبع("الخيارات[%s] = %s\n", الخيارات.المفتاح_عند(ع).صوان, الخيارات.القيمة_عند(ع).صوان)؛
    }
}

عرف نصوص_الترجمة: نـص = نـم.اقرأ_ملف(نـص(هات_مجلد_هذه_الشفرة[]) + الـعملية.اللغة + ".po")؛
إذا نصوص_الترجمة != "" تـرجمة.أضف_نصوصا(نصوص_الترجمة)؛

بـروجارج.هيئ()؛
بـروجارج.تعريف_الأمر = بـروجارج.تـعريف_الأمر().{
    المعطيات = تـطبيق[نـص, نـص]()
        .حدد(نـص("معطى1"), نـص("شرح المعطى 1."))
        .حدد(نـص("معطى2"), نـص("شرح المعطى 2."))؛
    الخيارات = تـطبيق[نـص, نـص]()
        .حدد(نـص("خيار1"), نـص("شرح الخيار 1."))
        .حدد(نـص("خيار2"), نـص("شرح الخيار 2."))؛
    معالج_التنفيذ = مغلفة (الخيارات: تـطبيق[نـص, نـص], المعطيات: مـصفوفة[نـص]) {
        // معالجة المعطيات والخيارات هنا.
        طـرفية.اطبع("معالجة الأمر الأساسي\ج")؛
        اطبع_خيارات(الخيارات)؛
        اطبع_معطيات(المعطيات)؛
    }؛
}
بـروجارج.حلل(2)؛
